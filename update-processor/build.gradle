import org.testcontainers.containers.PostgreSQLContainer

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.liquibase:liquibase-core:4.30.0'
        classpath 'org.testcontainers:postgresql:1.20.4'
    }
}

plugins {
    id 'org.jooq.jooq-codegen-gradle' version '3.19.14'
    id 'org.liquibase.gradle' version '3.0.1'
}

group = 'ru.tbank.processor'

tasks.register("postgresContainer") {
    var instance = new PostgreSQLContainer("postgres:17")
    instance.start()
    postgresContainer.ext.jdbcUrl = instance.getJdbcUrl()
    postgresContainer.ext.username = instance.getUsername()
    postgresContainer.ext.password = instance.getPassword()
}

liquibase {
    activities {
        main {
            changelogFile "db-changelog.yml"
            url postgresContainer.jdbcUrl
            username postgresContainer.username
            password postgresContainer.password
            searchPath "migrations\\db\\changelog"
            logLevel "info"
        }
    }
    jvmArgs "-Duser.dir=$project.rootDir"
}

jooq {
    configuration {
        jdbc {
            driver = 'org.postgresql.Driver'
            url = postgresContainer.jdbcUrl
            user = postgresContainer.username
            password = postgresContainer.password
        }

        generator {
            database {
                name = 'org.jooq.meta.postgres.PostgresDatabase'
                inputSchema = 'moderation'
            }
            target {
                packageName = 'ru.tbank.processor.generated'
            }
        }
    }
}

dependencies {
    // spring starters
    implementation 'org.springframework.boot:spring-boot-starter-amqp'
    implementation 'org.springframework.boot:spring-boot-starter-data-jdbc'
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    implementation 'org.springframework.boot:spring-boot-starter-jooq'

    // migration
    liquibaseRuntime 'org.liquibase:liquibase-core:4.30.0'
    liquibaseRuntime 'org.postgresql:postgresql:42.7.4'
    liquibaseRuntime 'info.picocli:picocli:4.7.6'

    // data
    runtimeOnly 'org.postgresql:postgresql:42.7.4'
    jooqCodegen 'org.postgresql:postgresql:42.7.4'
    implementation project(":common")

    // other
    implementation 'org.telegram:telegrambots-client:7.10.0'
    implementation 'org.apache.commons:commons-lang3:3.17.0'
}

tasks.compileJava {
    dependsOn(tasks.jooqCodegen)
}

tasks.jooqCodegen {
    dependsOn(tasks.postgresContainer)
    dependsOn(tasks.update)
}
